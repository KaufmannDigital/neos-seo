prototype(Neos.Seo:TitleTag) < prototype(Neos.Fusion:Component) {
    suffix = ${site.context.currentSite.name}

    title = Neos.Fusion:Case {
        titleOverride {
            condition = ${q(node).property('titleOverride')}
            renderer = ${q(node).property('titleOverride')}
            @position = 'start'
        }

        default {
            condition = true
            @position = 'end'
            renderer = Neos.Fusion:Collection {
                // Retrieve all parent document nodes excluding the homepage
                collection = ${q(documentNode).add(q(documentNode).parents('[instanceof Neos.Neos:Document]')).get()}
                collection.@process.removeSiteNode = ${Array.slice(value, 0, -1)}
                collection.@process.removeSiteNode.@if.onSubPage = ${documentNode != site}
                itemName = 'node'
                iterationName = 'nodeIterator'
                // Implode node titles with a dash
                itemRenderer = ${q(node).property('title') + (nodeIterator.isLast ? '' : ' - ')}
            }
        }

        @process.stripTags = ${String.stripTags(value)}
    }

    renderer = afx`
        <title>{(props.title ? props.title + ' - ' : '') + props.suffix}</title>
    `
}
