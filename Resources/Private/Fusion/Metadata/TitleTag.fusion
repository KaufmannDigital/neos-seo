prototype(Neos.Seo:TitleTag) < prototype(Neos.Fusion:Component) {
    suffix = ${site.context.currentSite.name}
    breadcrumbSeparator = ' - '
    @context.breadcrumbSeparator = ${this.breadcrumbSeparator}
    suffixSeparator = ${this.breadcrumbSeparator}

    title = Neos.Fusion:Case {
        titleOverride {
            condition = ${q(node).property('titleOverride')}
            renderer = ${q(node).property('titleOverride')}
            @position = 'start'
        }

        default {
            condition = true
            @position = 'end'
            renderer = Neos.Fusion:Loop {
                // Retrieve all parent document nodes excluding the homepage
                items = ${q(documentNode).add(q(documentNode).parents('[instanceof Neos.Neos:Document]')).get()}
                items.@process.removeSiteNode = ${Array.slice(value, 0, -1)}
                items.@process.removeSiteNode.@if.onSubPage = ${documentNode != site}
                itemName = 'node'
                iterationName = 'nodeIterator'
                // Implode node titles with the separator
                itemRenderer = ${q(node).property('title') + (nodeIterator.isLast ? '' : breadcrumbSeparator)}
            }
        }

        @process.stripTags = ${String.stripTags(value)}
    }

    renderer = afx`
        <title>{(props.title ? props.title + props.suffixSeparator : '') + props.suffix}</title>
    `
}
